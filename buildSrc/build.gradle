import org.ajoberstar.grgit.*
import org.ajoberstar.grgit.exception.*;

buildscript {
    dependencies {
        classpath 'org.ajoberstar:grgit:1.1.0'
    }
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: "groovy"
    
    rootProject.dependencies {
        runtimeOnly project(path)
    }

    it.repositories {
        mavenCentral()
    }

    it.dependencies {
        // These should really be under the projects themselves,
        // but when building on travis
        // the per project dependencies didn't seem to
        // get picked up. We only have one buildSrc plugin
        // so it doesn't really matter, but if we ever get more
        // we should figure this out
        implementation 'org.codehaus.groovy:groovy-all:3.0.2'
        implementation gradleApi()
        implementation 'org.yaml:snakeyaml:1.14'
    }
}

repositories {
    mavenCentral()
}


def edXAppPluginSetup(url, ref) {
    def buildSrc = project.file('plugins/')
    buildSrc.mkdirs()
    def dest = buildSrc.getAbsolutePath() + "/edx-app-gradle-plugin"

    if(!new File(dest).exists()) {
        Grgit.clone(dir: dest, uri: url, refToCheckout : ref)
    }

    def grgit = Grgit.open(dir : dest)
    try {
        grgit.fetch()
    }
    catch(GrgitException e) {
        println("Could not fetch. This is probably due to lack of an internet connection. Skipping")
    }
    grgit.checkout(branch : ref)
}

edXAppPluginSetup('https://github.com/edx/edx-app-gradle-plugin', '4c8c1e55bf2f124a6c89d9c8b4edbe0bd256f1a2')
